#include <FS.h>                   //this needs to be first, or it all crashes and burns...

#include <ESP8266WiFi.h>          //https://github.com/esp8266/Arduino

//needed for library
#include <DNSServer.h>
#include <ESP8266WebServer.h>
#include <WiFiManager.h>          //https://github.com/tzapu/WiFiManager

#include <ArduinoJson.h>          //https://github.com/bblanchon/ArduinoJson

/* Blynk */
#include <BlynkSimpleEsp8266.h>
#define BLYNK_PRINT Serial    // Comment this out to disable prints and save space

/* TIMER */
#include <SimpleTimer.h>
SimpleTimer timer;


/* DS18B20 Temperature Sensor */
#include <OneWire.h>
#include<DallasTemperature.h> 
#define ONE_WIRE_BUS 2 // DS18B20 on arduino pin2 corresponds to D4 on physical board
OneWire oneWire(ONE_WIRE_BUS);
DallasTemperature DS18B20(&oneWire);
float temp_0;
float temp_1;
float temp_2;

//define your default values here, if there are different values in config.json, they are overwritten.
char virtual_port_0[5] = "10";
char virtual_port_1[5] = "11";
char virtual_port_2[5] = "12";
char blynk_token[34] = "YOUR_BLYNK_TOKEN";

int vport_0;
int vport_1;
int vport_2;

//flag for saving data
bool shouldSaveConfig = false;

//callback notifying us of the need to save config
void saveConfigCallback () {
  Serial.println("Should save config");
  shouldSaveConfig = true;
}


void setup() {
  // put your setup code here, to run once:
  Serial.begin(115200);
  Serial.println();

  //clean FS, for testing
  //SPIFFS.format();

  //read configuration from FS json
  Serial.println("mounting FS...");

  if (SPIFFS.begin()) {
    Serial.println("mounted file system");
    if (SPIFFS.exists("/config.json")) {
      //file exists, reading and loading
      Serial.println("reading config file");
      File configFile = SPIFFS.open("/config.json", "r");
      if (configFile) {
        Serial.println("opened config file");
        size_t size = configFile.size();
        // Allocate a buffer to store contents of the file.
        std::unique_ptr<char[]> buf(new char[size]);

        configFile.readBytes(buf.get(), size);
        DynamicJsonBuffer jsonBuffer;
        JsonObject& json = jsonBuffer.parseObject(buf.get());
        json.printTo(Serial);
        if (json.success()) {
          Serial.println("\nparsed json");

          strcpy(virtual_port_0, json["virtual_port_0"]);
          strcpy(virtual_port_1, json["virtual_port_1"]);
          strcpy(virtual_port_2, json["virtual_port_2"]);
          strcpy(blynk_token, json["blynk_token"]);

        } else {
          Serial.println("failed to load json config");
        }
      }
    }
  } else {
    Serial.println("failed to mount FS");
  }
  //end read



  // The extra parameters to be configured (can be either global or just in the setup)
  // After connecting, parameter.getValue() will get you the configured value
  // id/name placeholder/prompt default length
  WiFiManagerParameter custom_blynk_token("blynk", "blynk token", blynk_token, 33);
  WiFiManagerParameter custom_virtual_port_0("port_1", "port 1", virtual_port_0, 3);
  WiFiManagerParameter custom_virtual_port_1("port_2", "port 2", virtual_port_1, 3);
  WiFiManagerParameter custom_virtual_port_2("port_3", "port 3", virtual_port_2, 3);

  //WiFiManager
  //Local intialization. Once its business is done, there is no need to keep it around
  WiFiManager wifiManager;

  //set config save notify callback
  wifiManager.setSaveConfigCallback(saveConfigCallback);

  //set static ip
  //wifiManager.setSTAStaticIPConfig(IPAddress(10,0,1,99), IPAddress(10,0,1,1), IPAddress(255,255,255,0));
  
  //add all your parameters here
  wifiManager.addParameter(&custom_blynk_token);
  wifiManager.addParameter(&custom_virtual_port_0);
  wifiManager.addParameter(&custom_virtual_port_1);
  wifiManager.addParameter(&custom_virtual_port_2);
  

  //reset settings - for testing
  //wifiManager.resetSettings();

  //set minimu quality of signal so it ignores AP's under that quality
  //defaults to 8%
  //wifiManager.setMinimumSignalQuality();
  
  //sets timeout until configuration portal gets turned off
  //useful to make it all retry or go to sleep
  //in seconds
  wifiManager.setTimeout(300);
 

  //fetches ssid and pass and tries to connect
  //if it does not connect it starts an access point with the specified name
  //here  "AutoConnectAP"
  //and goes into a blocking loop awaiting configuration
  if (!wifiManager.autoConnect("BeerTemp")) {
    Serial.println("failed to connect and hit timeout");
    delay(3000);
    //reset and try again, or maybe put it to deep sleep
    ESP.reset();
    delay(5000);
  }

  //if you get here you have connected to the WiFi
  Serial.println("connected...yeey :)");

  //read updated parameters
  strcpy(blynk_token, custom_blynk_token.getValue());
  strcpy(virtual_port_0, custom_virtual_port_0.getValue());
  strcpy(virtual_port_1, custom_virtual_port_1.getValue());
  strcpy(virtual_port_2, custom_virtual_port_2.getValue());

  //save the custom parameters to FS
  if (shouldSaveConfig) {
    Serial.println("saving config");
    DynamicJsonBuffer jsonBuffer;
    JsonObject& json = jsonBuffer.createObject();
    json["blynk_token"] = blynk_token;
    json["virtual_port_0"] = virtual_port_0;
    json["virtual_port_1"] = virtual_port_1;
    json["virtual_port_2"] = virtual_port_2;
    
    File configFile = SPIFFS.open("/config.json", "w");
    if (!configFile) {
      Serial.println("failed to open config file for writing");
    }

    json.printTo(Serial);
    json.printTo(configFile);
    configFile.close();
    //end save
  }

  Serial.println("local ip");
  Serial.println(WiFi.localIP());

  /* Blynk setup */

  Serial.print("Token: ");
  Serial.print(blynk_token);
  
  Blynk.config(blynk_token);
  Blynk.connect();
  DS18B20.begin();
  timer.setInterval(1000L, getSendData);
  Serial.println(" ");
  Serial.println("Testing Dual Sensor data");


}

void loop() {
  // put your main code here, to run repeatedly:
  timer.run(); // Initiates SimpleTimer
  Blynk.run();
}

/***************************************************
 * Send Sensor data to Blynk
 **************************************************/
void getSendData()
{
  DS18B20.requestTemperatures(); 
  temp_0 = DS18B20.getTempCByIndex(0); // Sensor 0 will capture Temp in Celcius
  temp_1 = DS18B20.getTempCByIndex(1); // Sensor 1 will capture Temp in Celcius
  temp_2 = DS18B20.getTempCByIndex(2); // Sensor 2 will capture Temp in Celcius


  Serial.print("Temp_0: ");
  Serial.print(temp_0);
  Serial.print(" oC . Temp_1: ");
  Serial.print(temp_1);
  Serial.print(" oC . Temp_2: ");
  Serial.print(temp_2);
  Serial.println(" oC");

  vport_0 = atoi (virtual_port_0);
  vport_1 = atoi (virtual_port_1);
  vport_2 = atoi (virtual_port_2);

      
  Blynk.virtualWrite(vport_0, temp_0); //virtual pin for sensor 0
  Blynk.virtualWrite(vport_1, temp_1); //virtual pin for sensor 1
  Blynk.virtualWrite(vport_2, temp_2); //virtual pin for sensor 2
}
